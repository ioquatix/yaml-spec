#!/usr/bin/env perl

use v5.18;

use XXX;  # XXX

my ($YYYY, $MM, $DD);

sub main {
  my ($front, $markdown) = read_files(@_);
  set_vars();

  my @sections;
  for my $section (parse_sections($markdown)) {
    my ($key) = keys %$section;
    my $method = "fmt_$key";
    $_ = $section->{$key};
    main->$method;
    push @sections, $_;
  }

  print "$front";
  print join "\n", @sections;
}

sub parse_sections {
  ($_) = @_;

  my @s;
  while ($_) {
    s/\A(#{1,4} .*\n)\n+//
      and push @s, {hx => $1} and next;
    s/\A(\*\*.*\*\*\n)\n+//
      and push @s, {hx => $1} and next;
    s/\A((?:\* .*\n)+(?:\{:\.\S+\}\n)?)\n+//
      and push @s, {ul => $1} and next;
    s/\A((?:(?:\*\*|-1 |\[[\w\"\*]|[\w\"\(]).*\n)+)\n+//
      and push @s, {p => $1} and next;
    s/\A(----\n)\n+//
      and push @s, {hr => $1} and next;
    s/\A\[%toc%\]\n\n+//
      and push @s, {toc => 1} and next;
    s/\A\[%index%\]\n//
      and push @s, {index => 1} and next;
    s/\A((?:>.*\n)+)\n+//
      and push @s, {ind => $1} and next;
    s/\A((?s:```.+?\n```\n))\n+//
      and push @s, {pre => $1} and next;
    s/\A(!\[.*\.png\)\n)\s+//
      and push @s, {img => $1} and next;
    s/\A(\* .*\n(?:  \S.*\n|\n(?=  \S))+)\n+//
      and push @s, {ul => $1} and next;
    s/\A(\`\w.*\n)\n+//
      and push @s, {p => $1} and next;
    s/\A((?:\| .*)+)\n+//
      and push @s, {tbl => $1} and next;
    s/\A((?:\* .*\n)(?:  .*\n|\n(?=  ))+)\n+//
      and push @s, {ul => $1} and next;

    s/((?:.*\n){20})(?s:.*)/$1/;
    WWW(\@s);
    die "------\n$_<<<";
  }

  return @s;
}

sub read_files {
  my ($front_file, $markdown_file) = @_;
  my $front = read_file($front_file);
  my $markdown = read_file($markdown_file);
  return ($front, $markdown)
}

sub read_file {
  my ($file) = @_;
  open my $fh, '<', $file;
  local $/;
  <$fh>;
}

sub set_vars {
  my ($d,$m,$y);
  ($_,$_,$_,$d,$m,$y) = localtime;
  $YYYY = 1900 + $y;
  $MM = sprintf "%02d", $m + 1;
  $DD = sprintf "%02d", $d;
}

sub fmt_hx {
  fmt_dates();
}
sub fmt_ul {
  fmt_undefined_links();
}
sub fmt_p {
  fmt_dates();
  fmt_undefined_links();
}
sub fmt_hr {}
sub fmt_ind {
  fmt_undefined_links();
}
sub fmt_pre {}
sub fmt_img {}
sub fmt_tbl {}

sub fmt_undefined_links {
  s/(\[[^\]]*?\]\(#\))/~~$1~~/sg;
}

sub fmt_dates {
  s/YYYY/$YYYY/g;
  s/MM/$MM/g;
  s/DD/$DD/g;
}

sub fmt_toc {
  $_ = <<'...';
**Contents**

* TOC
{:toc}
...
}

sub fmt_index {
  $_ = <<'...';
# Index
...
}

main @ARGV;
