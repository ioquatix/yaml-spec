#!/usr/bin/env coffee

require 'ingy-prelude'
Turndown = require 'turndown'
Node = require 'domino/lib/Node'
wrap = require 'smartwrap'

count = 0
skip = [
  'parentNode'
  'ownerDocument'
  '_nextSibling'
  '_previousSibling'
  '_attributeChangeHandlers'
]

getTexts = (node, a)->
  if node.nodeType == Node.TEXT_NODE
    a.push node._data
  else if node.nodeName == 'BR'
    a.push "\n  "
  else
    for n in node.childNodes
      getTexts n, a

getRowData = (tr)->
  a = []
  for td in tr.childNodes
    a.push _.trim(td.textContent) || ' '
  return a

class HtmlToMarkdown
  constructor: ->
    @turndown = new Turndown
      headingStyle: 'atx'
      preformattedCode: true
      codeBlockStyle: 'fenced'
      linkStyle: 'inlined'
      hr: '----'

    for k, v of @constructor.rules
      @turndown.addRule k, v

  convert: (html)->
    markdown = @turndown.turndown html
      .replace(/.*\n.*\n/, '')
      .replace(/^(###.*)\n\n</gm, '$1 <')
      .replace(/[“”]/g, '"')
      .replace(/"""/g, "'\"'")

  @tag_and_class: (tag, class_)->
    (n, o)->
      n.nodeName == tag.toUpperCase() and
      n.getAttribute('class') == class_

  @reformat_para: (m, n)->
      text = m.replace /\ \ $/mg, '%%%'
      text = text.replace /([\s\S]+?)(?:(\.)\ (?=[A-Z])|$)/g, (x, s, e)->
        e ?= ''
        wrap(s, width: 79) + "#{e}\n"
      text = text.replace /%%%$/mg, '  '
      return "#{text}\n\n"

  @rules:
    toc:
      filter: @tag_and_class 'div', 'toc'
      replacement: (m, n)->
        return "[%toc%]\n"

    informaltable:
      filter: @tag_and_class 'div', 'informaltable'
      replacement: (m, n)->
        table = ''
        for tr, i in n.getElementsByTagName 'TR'
          a = getRowData tr
          table += '| ' + a.join(' | ') + "\n"
          if i == 0
            table += '| ' + _.repeat('-- | ', a.length - 1) + "--\n"

        return table + "\n"

    br_example:
      filter: @tag_and_class 'br', 'example-break'
      replacement: (m, n)-> ''

    production:
      filter: (n, o)->
        n.nodeName == 'TR' and
        n.firstChild.getAttribute('class') == 'productioncounter'
      replacement: (m, n)->
        num = n.getElementsByClassName('productioncounter')[0].textContent
        lhs = n.getElementsByClassName('productionlhs')[0].textContent
        op  = n.getElementsByClassName('productionseperator')[0].textContent
        a = []
        getTexts(n.getElementsByClassName('productionrhs')[0], a)
        rhs = a.join ''
        if rhs.match /\ /
          rhs = "\n  #{rhs}"
        else
          op += ' '

        rhs = rhs.replace /\s+$/, ''

        return """
        ```
        #{num} #{lhs} #{op}#{rhs}
        ```\n\n
        """

    img:
      filter: 'img'
      replacement: (m, n)->
        XXX m

    pre:
      filter: 'pre'
      replacement: (m, n)->
        pre = m
          .replace(/^\\/gm, '')
          .replace(/\\([\[\]\*])/g, '$1')
          .replace(/\ +$/mg, '')
        return """
        ```
        #{pre}
        ```\n\n
        """

    p:
      filter: 'p'
      replacement: @reformat_para

    dd:
      filter: 'dd'
      replacement: @reformat_para

    code:
      filter: 'code'
      replacement: (m, n)->
        if m.match /^<.*mailto/
          return m
        else
          return "`#{m}`"

    xxx:
      filter: 'xxx'
      replacement: (m, n)->
        x = {}
        for k, v of n
          continue if k in skip
          x[k] = v
        www [m, x]
        xxx 42 if count++ > 10
        return


main = (spec)->
  to_markdown = new HtmlToMarkdown

  html = file_read spec

  out to_markdown.convert html


main process.argv[2..]...
